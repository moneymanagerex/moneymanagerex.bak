; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#ifndef AppExePath
        #error Use option -dAppExePath=path_to_your_exe
#endif

#ifndef BuildOpts
        #error Use option -dBuildOpts=value
#endif

#ifndef CpuArch
        #error Use option -dCpuArch=[x86|amd64|ia64]
#endif

#ifndef CrtDlls
        #error Use option -dCrtDlls=value
#endif


#define AppURL "http://www.codelathe.com/mmex"
#define AppExeName ExtractFileName( AppExePath )
#define OutputDir  ExtractFilePath( AppExePath )


; information from .exe GetVersionInfo

#define AppName GetStringFileInfo( AppExePath, PRODUCT_NAME )
#define VersionInfo GetFileVersion( AppExePath )
#define Copyright GetFileCopyright( AppExePath )
#define Company GetFileCompany( AppExePath )

[Setup]
AppName={#AppName}
AppVerName={#AppName} {#VersionInfo}
AppPublisher={#Company}
AppPublisherURL={#AppURL}
AppSupportURL={#AppURL}
AppUpdatesURL={#AppURL}

; {pf}\{#AppName}
DefaultDirName={userappdata}\{#AppName}

DefaultGroupName={#AppName}
DisableProgramGroupPage=yes
LicenseFile="..\..\runtime\license.txt"

OutputDir={#OutputDir}
OutputBaseFilename={#GetStringFileInfo( AppExePath, INTERNAL_NAME )}-{#VersionInfo}{#BuildOpts}-setup
Compression=lzma/ultra
SolidCompression=yes
SetupIconFile="..\..\runtime\mmex.ico"
AppCopyright={#Copyright}
AppId=37153D93-6D91-4763-82BB-0DF646211ED0
VersionInfoVersion={#VersionInfo}
VersionInfoCompany={#Company}
VersionInfoDescription={#GetFileDescription( AppExePath )}
VersionInfoCopyright={#Copyright}

; disable the "Select Setup Language" dialog and have it rely solely on auto-detection
;ShowLanguageDialog=no


; "ArchitecturesAllowed=x64" specifies that Setup cannot run on anything but x64
#if CpuArch == "x86"
  ArchitecturesAllowed=x86 x64
#elif CpuArch == "amd64"
  ArchitecturesAllowed=x64
#else
  ArchitecturesAllowed={#CpuArch}
#endif

; "ArchitecturesInstallIn64BitMode=x64 ia64" requests that the install
; be done in "64-bit mode" on x64 & Itanium, meaning it should use the
; native 64-bit Program Files directory and the 64-bit view of the
; registry. On all other architectures it will install in "32-bit mode".
ArchitecturesInstallIn64BitMode=x64 ia64


[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"
Name: "russian"; MessagesFile: "compiler:Languages\Russian.isl"
Name: "french";  MessagesFile: "compiler:Languages\French.isl"
Name: "german";  MessagesFile: "compiler:Languages\German.isl"
Name: "spanish"; MessagesFile: "compiler:Languages\Spanish.isl"
Name: "hebrew";  MessagesFile: "compiler:Languages\Hebrew.isl"
Name: "italian"; MessagesFile: "compiler:Languages\Italian.isl"
Name: "portuguese"; MessagesFile: "compiler:Languages\Portuguese.isl"
Name: "norwegian";  MessagesFile: "compiler:Languages\Norwegian.isl"

[Types]
Name: "full"; Description: "Full installation"
Name: "minimal"; Description: "Minimal installation"
Name: "custom"; Description: "Custom installation"; Flags: iscustom

[Components]
Name: "program"; Description: "Program Files"; Types: full minimal custom; Flags: fixed
Name: "help"; Description: "Help files"; Types: full minimal

Name: "lang"; Description: "Languages"; Types: full
Name: "lang\english"; Description: "English"; Types: full
Name: "lang\arabic"; Description: "Arabic"; Types: full
Name: "lang\bulgarian"; Description: "Bulgarian"; Types: full
Name: "lang\chinese_chs"; Description: "Chinese Chs"; Types: full
Name: "lang\chinese_zh"; Description: "Chinese Zh"; Types: full
Name: "lang\croatian"; Description: "Croatian"; Types: full
Name: "lang\czech"; Description: "Czech"; Types: full
Name: "lang\dutch"; Description: "Dutch"; Types: full
Name: "lang\dutch_be"; Description: "Dutch Be"; Types: full
Name: "lang\english_uk"; Description: "English UK"; Types: full
Name: "lang\french"; Description: "French"; Types: full
Name: "lang\german"; Description: "German"; Types: full
Name: "lang\greek"; Description: "Greek"; Types: full
Name: "lang\hebrew"; Description: "Hebrew"; Types: full
Name: "lang\hungarian"; Description: "Hungarian"; Types: full
Name: "lang\indonesian"; Description: "Indonesian"; Types: full
Name: "lang\italian"; Description: "Italian"; Types: full
Name: "lang\latvian"; Description: "Latvian"; Types: full
Name: "lang\norwegian"; Description: "Norwegian"; Types: full
Name: "lang\polish"; Description: "Polish"; Types: full
Name: "lang\portuguese"; Description: "Portuguese"; Types: full
Name: "lang\portuguese_portugal"; Description: "Portuguese Portugal"; Types: full
Name: "lang\romanian"; Description: "Romanian"; Types: full
Name: "lang\russian"; Description: "Russian"; Types: full
Name: "lang\serbo_croatian"; Description: "Serbo Croatian"; Types: full
Name: "lang\slovak"; Description: "Slovak"; Types: full
Name: "lang\slovenian"; Description: "Slovenian"; Types: full
Name: "lang\spanish"; Description: "Spanish"; Types: full
Name: "lang\swedish"; Description: "Swedish"; Types: full
Name: "lang\tamil"; Description: "Tamil"; Types: full
Name: "lang\turkish"; Description: "Turkish"; Types: full
Name: "lang\ukrainian"; Description: "Ukrainian"; Types: full
Name: "lang\vietnamese"; Description: "Vietnamese"; Types: full


[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked
Name: "quicklaunchicon"; Description: "{cm:CreateQuickLaunchIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked


[Files]
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

#define lang_dir "{app}\en"

Source: "..\..\runtime\en\english.*";          DestDir: {#lang_dir}; Components: "lang\english";       Flags: ignoreversion
Source: "..\..\runtime\en\arabic.*";           DestDir: {#lang_dir}; Components: "lang\arabic";        Flags: ignoreversion
Source: "..\..\runtime\en\bulgarian.*";        DestDir: {#lang_dir}; Components: "lang\bulgarian";     Flags: ignoreversion
Source: "..\..\runtime\en\chinese_chs.*";      DestDir: {#lang_dir}; Components: "lang\chinese_chs";   Flags: ignoreversion
Source: "..\..\runtime\en\chinese_zh.*";       DestDir: {#lang_dir}; Components: "lang\chinese_zh";    Flags: ignoreversion
Source: "..\..\runtime\en\croatian.*";         DestDir: {#lang_dir}; Components: "lang\croatian";      Flags: ignoreversion
Source: "..\..\runtime\en\czech.*";            DestDir: {#lang_dir}; Components: "lang\czech";         Flags: ignoreversion
Source: "..\..\runtime\en\dutch.*";            DestDir: {#lang_dir}; Components: "lang\dutch";         Flags: ignoreversion
Source: "..\..\runtime\en\dutch_be.*";         DestDir: {#lang_dir}; Components: "lang\dutch_be";      Flags: ignoreversion
Source: "..\..\runtime\en\english-uk.*";       DestDir: {#lang_dir}; Components: "lang\english_uk";    Flags: ignoreversion
Source: "..\..\runtime\en\french.*";           DestDir: {#lang_dir}; Components: "lang\french";        Flags: ignoreversion
Source: "..\..\runtime\en\german.*";           DestDir: {#lang_dir}; Components: "lang\german";        Flags: ignoreversion
Source: "..\..\runtime\en\greek.*";            DestDir: {#lang_dir}; Components: "lang\greek";         Flags: ignoreversion
Source: "..\..\runtime\en\hebrew.*";           DestDir: {#lang_dir}; Components: "lang\hebrew";        Flags: ignoreversion
Source: "..\..\runtime\en\hungarian.*";        DestDir: {#lang_dir}; Components: "lang\hungarian";     Flags: ignoreversion
Source: "..\..\runtime\en\indonesian.*";       DestDir: {#lang_dir}; Components: "lang\indonesian";    Flags: ignoreversion
Source: "..\..\runtime\en\italian.*";          DestDir: {#lang_dir}; Components: "lang\italian";       Flags: ignoreversion
Source: "..\..\runtime\en\latvian.*";          DestDir: {#lang_dir}; Components: "lang\latvian";       Flags: ignoreversion
Source: "..\..\runtime\en\norwegian.*";        DestDir: {#lang_dir}; Components: "lang\norwegian";     Flags: ignoreversion
Source: "..\..\runtime\en\polish.*";           DestDir: {#lang_dir}; Components: "lang\polish";        Flags: ignoreversion
Source: "..\..\runtime\en\portuguese.*";       DestDir: {#lang_dir}; Components: "lang\portuguese";    Flags: ignoreversion
Source: "..\..\runtime\en\portuguese_portugal.*"; DestDir: {#lang_dir}; Components: "lang\portuguese_portugal"; Flags: ignoreversion
Source: "..\..\runtime\en\romanian.*";         DestDir: {#lang_dir}; Components: "lang\romanian";      Flags: ignoreversion
Source: "..\..\runtime\en\russian.*";          DestDir: {#lang_dir}; Components: "lang\russian";       Flags: ignoreversion
Source: "..\..\runtime\en\serbo-croatian.*";   DestDir: {#lang_dir}; Components: "lang\serbo_croatian"; Flags: ignoreversion
Source: "..\..\runtime\en\slovak.*";           DestDir: {#lang_dir}; Components: "lang\slovak";        Flags: ignoreversion
Source: "..\..\runtime\en\Slovenian.*";        DestDir: {#lang_dir}; Components: "lang\slovenian";     Flags: ignoreversion
Source: "..\..\runtime\en\spanish.*";          DestDir: {#lang_dir}; Components: "lang\spanish";       Flags: ignoreversion
Source: "..\..\runtime\en\swedish.*";          DestDir: {#lang_dir}; Components: "lang\swedish";       Flags: ignoreversion
Source: "..\..\runtime\en\tamil.*";            DestDir: {#lang_dir}; Components: "lang\tamil";         Flags: ignoreversion
Source: "..\..\runtime\en\turkish.*";          DestDir: {#lang_dir}; Components: "lang\turkish";       Flags: ignoreversion
Source: "..\..\runtime\en\ukrainian.*";        DestDir: {#lang_dir}; Components: "lang\ukrainian";     Flags: ignoreversion
Source: "..\..\runtime\en\vietnamese.*";       DestDir: {#lang_dir}; Components: "lang\vietnamese";    Flags: ignoreversion

#define help_dir "{app}\help"

Source: "..\..\docs\*.jpg"; DestDir: {#help_dir}; Components: help; Flags: ignoreversion
Source: "..\..\docs\*.png"; DestDir: {#help_dir}; Components: help; Flags: ignoreversion
Source: "..\..\docs\investment.html"; DestDir: {#help_dir}; Components: help; Flags: ignoreversion
Source: "..\..\docs\index.html"; Languages: english; DestDir: {#help_dir}; Components: help; Flags: ignoreversion
Source: "..\..\docs\french-help.html";  Languages: french; DestName: "index.html"; DestDir: {#help_dir}; Components: help; Flags: ignoreversion
Source: "..\..\docs\index_fichiers\*";  Languages: french; DestDir: "{#help_dir}\index_fichiers"; Components: help; Flags: ignoreversion
Source: "..\..\docs\italian-help.html"; Languages: italian; DestName: "index.html"; DestDir: {#help_dir}; Components: help; Flags: ignoreversion
Source: "..\..\docs\russian-help.html"; Languages: russian; DestName: "index.html"; DestDir: {#help_dir}; Components: help; Flags: ignoreversion

Source: "..\..\runtime\*"; DestDir: "{app}"; Components: program; Flags: ignoreversion
; .exe already compressed by UPX
Source: {#AppExePath}; DestDir: "{app}"; Components: program; Flags: ignoreversion nocompression
Source: {#CrtDlls}; DestDir: "{app}"; Components: program; Flags: ignoreversion


[Icons]
Name: "{group}\{#AppName}"; Filename: "{app}\{#AppExeName}"; IconFilename: "{app}\{#AppExeName}"; WorkingDir: {app}; IconIndex: 0
Name: "{group}\{cm:UninstallProgram,{#AppName}}"; Filename: "{uninstallexe}"
; Name: "{commondesktop}\{#AppName}"; Filename: "{app}\{#AppExeName}"; Tasks: desktopicon
Name: "{userdesktop}\{#AppName}"; Filename: "{app}\{#AppExeName}"; Tasks: desktopicon; IconFilename: "{app}\{#AppExeName}"; WorkingDir: {app}; IconIndex: 0
Name: "{userappdata}\Microsoft\Internet Explorer\Quick Launch\{#AppName}"; Filename: "{app}\{#AppExeName}"; Tasks: quicklaunchicon; WorkingDir: {app}; IconFilename: "{app}\{#AppExeName}"; IconIndex: 0

[Run]
Filename: "{app}\{#AppExeName}"; Description: "{cm:LaunchProgram,{#AppName}}"; Flags: nowait postinstall skipifsilent

[UninstallDelete]
Type: files; Name: "{app}\mmexini.db3"
Type: files; Name: "{app}\*.png"
Type: files; Name: "{app}\*.txt"

[Messages]
WelcomeLabel1=Welcome to [name] Setup
